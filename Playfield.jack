/** Implements a playfield */
class Playfield {

  field int gridWidth;
  field int gridHeight;
  field Array grid;

  /** Constructs a new Playfield. */
  constructor Playfield new() {
    var int i;
    let gridWidth = 10;
    let gridHeight = 18;
    let grid = Array.new(gridHeight);
    let i = 0;
    while (i < gridHeight) {
      let grid[i] = Array.new(gridWidth);
      let i = i + 1;
    }
    return this;
  }

  /** Disposes this playfield. */
  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }

  /** Clears the playfield. */
  method void clear() {
    var int i;
    var int j;
    var Array row;
    var Square s;
    let i = 0;
    while (i < gridHeight) {
      let row = grid[i];
      let j = 0;
      while (j < gridWidth) {
        if (~(row[j] = null)) {
          let s = row[j];
          do s.erase();
          let row[j] = null;
        }
        let j = j + 1;
      }
      let i = i + 1;
    }
    return;
  }

  /** Gets the playfield width. */
  method int getWidth() {
    return gridWidth;
  }

  /** Gets the playfield height. */
  method int getHeight() {
    return gridHeight;
  }

  /** Gets the grid. */
  method Array getGrid() {
    return grid;
  }

  /** Gets a square from the playfield. */
  method Square getSquare(int x, int y) {
    var Array row;
    let row = grid[y];
    return row[x];
  }

  /** Sets a tetromino on the playfield. */
  method void setTetromino(Tetromino tetromino) {
    var int i;
    var int xpos;
    var int ypos;
    var Array row;
    var Array squares;
    var Square square;
    let i = 0;
    let squares = tetromino.getSquares();
    while (i < 4) {
      let square = squares[i];
      let ypos = square.getYpos();
      let row = grid[ypos];
      let row[square.getXpos()] = square;
      let i = i + 1;
    }
    return;
  }

  /** Checks if there is space for a tetromino in the given direction */
  method boolean isSpace(Tetromino tetromino, int direction) {
    var boolean isSpace;
    var int xpos;
    var int ypos;
    var int xdest;
    var int ydest;
    let isSpace = false;
    let xpos = tetromino.getXpos();
    let ypos = tetromino.getYpos();
    if (direction = 2) {
      let ydest = ypos + 1;
      if (ydest < (getHeight() - 1)) {
        if ((getSquare(xpos, ydest + 1) = null) & (getSquare(xpos + 1, ydest + 1) = null)) {
          let isSpace = true;
        }
      }
    }
    if (direction = 3) {
      let xdest = xpos - 1;
      if (xdest > (-(1))) {
        if ((getSquare(xpos - 1, ypos) = null) & (getSquare(xpos - 1, ypos + 1) = null)) {
          let isSpace = true;
        }
      }
    }
    if (direction = 4) {
      let xdest = xpos + 1;
      if ((xdest + 1) < getWidth()) {
        if ((getSquare(xpos + 1, ypos) = null) & (getSquare(xpos + 1, ypos + 1) = null)) {
          let isSpace = true;
        }
      }
    }
    return isSpace;
  }

  /** Clears the bottom row after scoring. */
  method void clearRow(int rowIndex) {
    var int j;
    var Array row;
    var Square s;
    let j = 0;
    let row = grid[rowIndex];
    while (j < gridWidth) {
      let s = row[j];
      do s.erase();
      do s.dispose();
      let s = null;
      let row[j] = null;
      let j = j + 1;
    }
    return;
  }

  /** Shifts a row down */
  method void shiftRow(int rowIndex) {
    var int j;
    var Array row;
    var Array nextRow;
    var Square s;
    let row = grid[rowIndex];
    let nextRow = grid[rowIndex + 1];
    let j = 0;
    while (j < gridWidth) {
      if (~(row[j] = null)) {
        let s = row[j];
        do s.moveDown();
        let row[j] = null;
        let nextRow[j] = s;
      }
      let j = j + 1;
    }
    return;
  }

  /** Checks if there is space for a new tetromino */
  method boolean isFull() {
    var Array row;
    var boolean full;
    let full = false;
    let row = grid[1];
    if (row[4] | row[5]) {
      let full = true;
    }
    return full;
  }
}