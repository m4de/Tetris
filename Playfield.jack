/** Implements a playfield */
class Playfield {

  field int gridWidth;
  field int gridHeight;
  field Array grid;

  field Square square;

  /** Constructs a new Playfield. */
  constructor Playfield new() {
    var int i;
    let gridWidth = 10;
    let gridHeight = 18;
    let grid = Array.new(gridHeight);
    let i = 0;
    while (i < gridHeight) {
      let grid[i] = Array.new(gridWidth);
      let i = i + 1;
    }
    return this;
  }

  /** Disposes this playfield. */
  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }

  /** Clears the playfield. */
  method void clear() {
    var int i;
    var int j;
    var Array row;
    var Square s;
    let i = 0;
    while (i < gridHeight) {
      let row = grid[i];
      let j = 0;
      while (j < gridWidth) {
        if (~(row[j] = null)) {
          let s = row[j];
          do s.erase();
          let row[j] = null;
        }
        let j = j + 1;
      }
      let i = i + 1;
    }
    if (~(square = null)) {
      do square.erase();
    }
    return;
  }

  /** Gets the playfield width. */
  method int getWidth() {
    return gridWidth;
  }

  /** Gets the playfield height. */
  method int getHeight() {
    return gridHeight;
  }

  /** Gets the grid. */
  method Array getGrid() {
    return grid;
  }

  /** Gets a square from the playfield. */
  method Square getSquare(int x, int y) {
    var Array row;
    let row = grid[y];
    return row[x];
  }

  /** Sets a square on the playfield. */
  method void setSquare(int x, int y) {
    var Array row;
    let row = grid[y];
    let row[x] = square;
    return;
  }

  /** Puts a new square from the playfield. */
  method void newSquare() {
    var int x;
    var int y;
    var Array row;
    let x = 4;
    let y = 0;
    let row = grid[0];
    let row[4] = Square.new(x,y);
    let square = row[4];
    return;
  }

  method void moveDown() {
    do square.moveDown();
    return;
  }

  method void moveLeft() {
    do square.moveLeft();
    return;
  }

  method void moveRight() {
    do square.moveRight();
    return;
  }

  method Square getCurrentSquare() {
    return square;
  }

  /** Clears the bottom row after scoring. */
  method void clearRow(int rowIndex) {
    var int j;
    var Array row;
    var Square s;
    let j = 0;
    let row = grid[rowIndex];
    while (j < gridWidth) {
      let s = row[j];
      do s.erase();
      do s.dispose();
      let s = null;
      let row[j] = null;
      let j = j + 1;
    }
    return;
  }

  /** Shifts a row down */
  method void shiftRow(int rowIndex) {
    var int j;
    var Array row;
    var Array nextRow;
    var Square s;
    let row = grid[rowIndex];
    let nextRow = grid[rowIndex + 1];
    let j = 0;
    while (j < gridWidth) {
      if (~(row[j] = null)) {
        let s = row[j];
        do s.moveDown();
        let row[j] = null;
        let nextRow[j] = s;
      }
      let j = j + 1;
    }
    return;
  }
}