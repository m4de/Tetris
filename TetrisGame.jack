/** Implements a Tetris game. */
class TetrisGame {

  field int down;
  field int left;
  field int right;

  field boolean exit;
  field boolean paused;
  field boolean gameOver;

  field Playfield playfield;
  field Tetromino tetromino;
  field int nextType;

  field int level;
  field int lines;

  field int samplerate;
  field int counter;
  field int delay;

  /** Constructs a new Tetris game. */
  constructor TetrisGame new() {
    let down = 2;
    let left = 3;
    let right = 4;
    let exit = false;
    let paused = false;
    let gameOver = true;
    let playfield = Playfield.new();
    let samplerate = 100;
    let delay = 1000;
    do Random.setSeed(1638);
    let nextType = Random.randRange(6);
    return this;
  }

  /** Disposes this game. */
  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }

  /** Runs the game: handles the userâ€™s inputs and moves the tetrominoes accordingly */
  method void run() {
    var char key; // the key currently pressed by the user

    // draw the user interface
    do drawUI();

    while (~exit) {
      // waits for a key to be pressed
      while (key = 0) {
        let key = Keyboard.keyPressed();
      }
      if (key = 78) { do newGame(); }  // n key
      if (key = 81) { do quit(); }    // q key

      // waits for the key to be released
      while (~(key = 0)) {
        let key = Keyboard.keyPressed();
      }
    } // while
    return;
  }

  /** Draws the user interface. */
  method void drawUI() {
    // print logo
    do Utils.printString("TTT EEE TTT RR  I SSS", 2, 1);
    do Utils.printString(" T  E    T  R R I S  ", 3, 1);
    do Utils.printString(" T  EE   T  RR  I SSS", 4, 1);
    do Utils.printString(" T  E    T  R R I   S", 5, 1);
    do Utils.printString(" T  EEE  T  R R I SSS", 6, 1);
    // print menu
    do Utils.printString("[N]ew Game", 10, 6);
    do Utils.printString("[P]ause", 12, 6);
    do Utils.printString("<=> Move", 14, 6);
    do Utils.printString("SB Rotate", 16, 6);
    do Utils.printString("[Q]uit", 18, 6);
    // draw playing field
    do Screen.drawRectangle(181, 0, 185, 255);
    do Screen.drawRectangle(186, 253, 326, 255);
    do Screen.drawRectangle(327, 0, 331, 255);
    // print right menu
    do Utils.printString("Level:", 3, 50);
    do Utils.printString("Lines:", 8, 50);
    do Utils.printString("Next:", 15, 50);
    return;
  }

  /** Starts a new game. */
  method void newGame() {
    var char key; // the key currently pressed by the user

    let gameOver = false;
    do playfield.clear();
    if (~(tetromino = null)) {
      do tetromino.erase();
    }

    let level = 0;
    do Utils.printInt(level, 5, 52);
    let lines = 0;
    do Utils.printInt(lines, 10, 52);

    do Utils.printString("             ", 10, 46);

    let tetromino = Tetromino.new(nextType);
    let nextType = Random.randRange(6);
    do drawNextTetromino();

    do pause(false);

    let counter = 0;

    while (~exit) {
      let key = Keyboard.keyPressed();

      if (key = 32) { do rotateTetromino(); }     // space bar
      if (key = 78) { do newGame(); }             // n key
      if (key = 80) { do pause(~paused); }        // p key
      if (key = 81) { do quit(); }                // q key
      if (key = 130) { do moveTetromino(left); }  // left arrow
      if (key = 132) { do moveTetromino(right); } // right arrow
      if (key = 133) { do moveTetromino(down); }  // down arrow

      do Sys.wait(samplerate);

      let counter = counter + samplerate;
      if (Utils.modulo(counter, delay) = 0) {
        do moveTetromino(down);
        let counter = 0;
      }
    }
    return;
  }

  /** (Un)pauses the game. */
  method void pause(boolean pause) {
    if (~gameOver) {
      if (pause) {
        let paused = true;
        let samplerate = 0;
        do Utils.printString("* PAUSED *", 12, 48);
      } else {
        let paused = false;
        let samplerate = 100;
        do Utils.printString("          ", 12, 48);
      }
    }
    return;
  }

  /** Quits the game. */
  method void quit() {
    if (~paused) {
      do pause(true);
    }
    do Screen.clearScreen();
    let exit = true;
    return;
  }

  /** Moves the tetromino across the playfield. */
  method void moveTetromino(int direction) {
    if (direction = down) {
      if (playfield.isSpace(tetromino, down)) {
        do tetromino.move(down);
      } else {
        do playfield.setTetromino(tetromino);
        do checkScore();
        do checkGameOver();
      }
    } else {
      if (playfield.isSpace(tetromino, direction)) {
        do tetromino.move(direction);
      }
    }
    return;
  }

  /** Rotates the tetromino if possible. */
  method void rotateTetromino() {
    if (playfield.canRotate(tetromino)) {
      do tetromino.rotate();
    }
    return;
  }

  /** Checks if the bottom row is full. */
  method void checkScore() {
    var int i;
    var int j;
    var int k;
    var boolean full;
    var Array grid;
    var Array row;
    let grid = playfield.getGrid();
    let i = 0;
    while (i < playfield.getHeight()) {
      let row = grid[i];
      let full = true;
      let j = 0;
      while (j < playfield.getWidth()) {
        if (row[j] = null) {
           let full = false;
        }
        let j = j + 1;
      }
      if (full) {
        do playfield.clearRow(i);
        let k = i;
        while (k > 0) {
          do playfield.shiftRow(k);
          let k = k - 1;
        }
        let lines = lines + 1;
        if (Utils.modulo(lines, 10) = 0) {
          let level = level + 1;
          do Utils.printInt(level, 5, 52);
          let delay = delay - 100;
        }
      }
      let i = i + 1;
    }
    do Utils.printInt(lines, 10, 52);
    return;
  }

  /** Checks if the game is over. */
  method void checkGameOver() {
    if (playfield.isFull()) {
      do Utils.printString("* GAME OVER *", 12, 46);
    } else {
      let tetromino = Tetromino.new(nextType);
      let nextType = Random.randRange(6);
      do drawNextTetromino();
    }
    return;
  }

  /** Draws the next tetromino on the screen. */
  method void drawNextTetromino() {
    // erase the previous one
    do Screen.setColor(false);
    do Screen.drawRectangle(410-14, 190, 410 + 42, 190 + 42);

    do Screen.setColor(true);
    if (nextType = 0) {
      do Screen.drawRectangle(410, 190, 410 + 28, 190 + 28);
    }
    if (nextType = 1) {
      do Screen.drawRectangle(410 - 14, 190, 410 + 42, 190 + 14);
    }
    if (nextType = 2) {
      do Screen.drawRectangle(410 - 14, 190, 410 + 28, 190 + 14);
      do Screen.drawRectangle(410, 190 + 14, 410 + 14, 190 + 28);
    }
    if (nextType = 3) {
      do Screen.drawRectangle(410 + 14, 190, 410 + 28, 190 + 42);
      do Screen.drawRectangle(410, 190 + 28, 410 + 14, 190 + 42);
    }
    if (nextType = 4) {
      do Screen.drawRectangle(410, 190, 410 + 14, 190 + 42);
      do Screen.drawRectangle(410 + 14, 190 + 28, 410 + 28, 190 + 42);
    }
    if (nextType = 5) {
      do Screen.drawRectangle(410, 190, 410 + 28, 190 + 14);
      do Screen.drawRectangle(410 - 14, 190 + 14, 410 + 14, 190 + 28);
    }
    if (nextType = 6) {
      do Screen.drawRectangle(410 - 14, 190, 410 + 14, 190 + 14);
      do Screen.drawRectangle(410, 190 + 14, 410 + 28, 190 + 28);
    }
    return;
  }
}
