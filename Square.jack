/** Implements a graphical square. */
class Square {

  field int x, y; // screen location of the square's top-left corner
  field int size; // length of this square, in pixels

  field int xpos, ypos; // position of the square in the 10*18 grid

  /** Constructs a new square with a given location and size. */
  constructor Square new(int Axpos, int Aypos) {
    let size = 14;
    let x = (512 / 2) - size;
    let y = 3;
    let xpos = Axpos;
    let ypos = Aypos;
    do draw();
    return this;
  }

  /** Disposes this square. */
  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }

  /** Draws the square on the screen. */
  method void draw() {
    do Screen.setColor(true);
    do Screen.drawRectangle(x, y, x + size, y + size);
    return;
  }

  /** Erases the square from the screen. */
  method void erase() {
    do Screen.setColor(false);
    do Screen.drawRectangle(x, y, x + size, y + size);
    return;
  }

  /** Moves the square down by `size` pixels. */
  method void moveDown() {
    if (ypos < 17) {
      do erase();
      let y = y + size;
      do draw();
      let ypos = ypos + 1;
    }
    return;
  }

  /** Moves the square left by `size` pixels. */
  method void moveLeft() {
    if (xpos > 0) {
      do erase();
      let x = x - size;
      do draw();
      let xpos = xpos - 1;
    }
    return;
  }

  /** Moves the square right by `size` pixels. */
  method void moveRight() {
    if (xpos < 9) {
      do erase();
      let x = x + size;
      do draw();
      let xpos = xpos + 1;
    }
    return;
  }

  method int getXpos() {
    return xpos;
  }

  method int getYpos() {
    return ypos;
  }
}
