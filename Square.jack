/** Implements a graphical square. */
class Square {

  field int x, y; // screen location of the square's top-left corner
  field int size; // length of this square, in pixels

  /** Constructs a new square with a given location and size. */
  constructor Square new(int Ax, int Ay, int Asize) {
    let x = Ax;
    let y = Ay;
    let size = Asize;
    do draw();
    return this;
  }

  /** Disposes this square. */
  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }

  /** Draws the square on the screen. */
  method void draw() {
    do Screen.setColor(true);
    do Screen.drawRectangle(x, y, x + size, y + size);
    return;
  }

  /** Erases the square from the screen. */
  method void erase() {
    do Screen.setColor(false);
    do Screen.drawRectangle(x, y, x + size, y + size);
    return;
  }

  /** Moves the square down by `size` pixels. */
  method boolean moveDown() {
    var boolean isDown;
    if (y < (255 - (size * 2))) {
      do erase();
      let y = y + size;
      do draw();
      let isDown = false;
    } else {
      let isDown = true;
    }
    return isDown;
  }

  /** Moves the square left by `size` pixels. */
  method void moveLeft() {
    if (x > 186) {
      do erase();
      let x = x - size;
      do draw();
    }
    return;
  }

  /** Moves the square right by `size` pixels. */
  method void moveRight() {
    if (x < 312) {
      do erase();
      let x = x + size;
      do draw();
    }
    return;
  }
}
